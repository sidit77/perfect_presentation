plugins {
    id 'base'
}

def release = providers.gradleProperty("release").isPresent()

def cargoBuild = { String arch, String javaArch, String target ->
    tasks.register("build${arch}", Exec) {
        group = "build"
        description = "Build Rust library for ${arch}"

        workingDir = project.projectDir

        commandLine "cargo", "build", "--target", target, "--profile", (release ? "release" : "dev")

        inputs.files("Cargo.toml", "Cargo.lock")
        inputs.dir("src")

        outputs.file file("$workingDir/target/${target}/${release ? "release" : "debug"}/perfect_presentation.dll")

        onlyIf {
            release || System.getProperty("os.arch").equals(javaArch)
        }
    }
}

def buildX64   = cargoBuild("X64", "amd64", "x86_64-pc-windows-msvc")
def buildArm64 = cargoBuild("Arm64", "arm64", "aarch64-pc-windows-msvc")

artifacts {
    add("default", buildX64) {
        classifier = "x64"
    }

    add("default", buildArm64) {
        classifier = "arm64"
    }
}

tasks.register("cargoClean", Exec) {
    group = "build"
    description = "Clean Cargo build outputs"

    workingDir = project.projectDir
    commandLine "cargo", "clean"
}

tasks.named("clean") {
    dependsOn("cargoClean")
}
